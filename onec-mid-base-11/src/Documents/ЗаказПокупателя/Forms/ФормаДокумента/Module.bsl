
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды 
	
	//++Кузнецова М.С. 16.10.2023
	//Добавлены на форму элементы Добав_КонтактноеЛицо, Добав_КонтактноеЛицо,С учетом скидки.
	
	 ЭлементКонтактноеЛицо = Элементы.Вставить("ЭлементКонтактноеЛицо",Тип("ПолеФормы"),Элементы.ГруппаШапкаПраво,);
	 ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.Добав_КонтактноеЛицо";
	 ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	 ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо";
	 
	 ЭлементСогласованнаяСкидка = Элементы.Вставить("ЭлементСогласованнаяСкидка",Тип("ПолеФормы"),Элементы.ГруппаШапкаЛево,);
	 ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.Добав_СогласованнаяСкидка";
	 ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	 ЭлементСогласованнаяСкидка.Заголовок = "Согласованная скидка";
	 ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении","Добав_СогласованнаяСкидкаПриИзменении");
	 
	 Команда = Команды.Добавить("Добав_СУчетомСкидкиПеред");
	 Команда.Заголовок = "С учетом скидки";
	 Команда.Действие = "Добав_СУчетомСкидкиПеред";
	 
	 КнопкаФормы = Элементы.Вставить("СУчетомСкидки",Тип("КнопкаФормы"),Элементы.ГруппаШапкаЛево,);
	 КнопкаФормы.ИмяКоманды = "Добав_СУчетомСкидкиПеред";
	 КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
	 //--	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти


&НаКлиенте
Процедура Добав_СогласованнаяСкидкаПриИзменении(Элемент)
	
	//++Кузнецова М.С. 16.10.2023 
	//Добавлено событие при изменении согласованной скидки 
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса",ЭтотОбъект,Параметры);
	ПоказатьВопрос(Оповещение, "Размер скидки изменился.Произвести пересчет?",Режим,,,"Пересчет",);
	
	//--
	
КонецПроцедуры 

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	//++Кузнецова М.С. 16.10.2023 
	//После вопроса при событии Добав_СогласованнаяСкидкаПриИзменении 

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		СУчетомСкидкиПеред();
	КонецЕсли;
	
	//--
		
КонецПроцедуры


 &НаКлиенте
 Процедура СУчетомСкидкиПеред()
	 
	 //++Кузнецова М.С. 16.10.2023 
	//Пересчет с учетом согласованной скидки для табличных частей  
 
	 Для Каждого Строка Из Объект.Товары Цикл
		 Строка.Сумма = (Строка.Цена * Строка.Количество) * (1 - Объект.Добав_СогласованнаяСкидка/100);	 
	 КонецЦикла; 
	 
	 Для Каждого Строка Из Объект.Услуги Цикл
		 Строка.Сумма = (Строка.Цена * Строка.Количество) * (1 - Объект.Добав_СогласованнаяСкидка/100);	 
	 КонецЦикла;
	 
	 //--
		 
КонецПроцедуры
 
&НаКлиенте
 Процедура Добав_СУчетомСкидкиПеред(Команда)
	 
	 //++Кузнецова М.С. 16.10.2023 
	//Команда для пересчета скидки
	
	 СУчетомСкидкиПеред();
	 
	 //--
 
 КонецПроцедуры


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	 //++Кузнецова М.С. 16.10.2023 
	 //Исходный код:
	 //ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	 //Новый код:
   	 ТекущиеДанные.Сумма = (ТекущиеДанные.Цена * ТекущиеДанные.Количество) *(1 - Объект.Добав_СогласованнаяСкидка/100);;
	//--	 
	 
	 РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти 

